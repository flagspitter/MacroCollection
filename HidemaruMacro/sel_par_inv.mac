#org_x = column + 1;
#org_y = lineno;

disabledraw;

escape;
#point = 1;

left;
#startcode = code;
right;


//
// 開始位置→外郭の ( に移動
//
	while( #point > 0 )
	{
		#ccode = colorcode & 0x0000001f;
		if( (#ccode != 3) && (#ccode != 20) )
		{
			if( code == ')' )
			{
				#point = #point - 1;
			}
			if( code == '(' )
			{
				#point = #point + 1;
			}
			
			if( (code == '}') || (code == '{') || (code == eof) || (code == ';'))
			{
				movetolineno #org_x, #org_y;
				call selcurrent;
				endmacro;
			}
		}
		right;
	}

//
// ( → 対応する ) に移動
//
	#point = 1;
	
	#startsel_x = column + 1;
	#startsel_y = lineno;
	
	left;
	while( #point > 0 )
	{
		left;
		
		#ccode = colorcode & 0x0000001f;
		if( (#ccode != 3) && (#ccode != 20) )
		{
			if( code == ')' )
			{
				#point = #point + 1;
			}
			if( code == '(' )
			{
				#point = #point - 1;
			}
			
			if( (code == '}') || (code == '{') || (code == ';') )
			{
				movetolineno #org_x, #org_y;
				call selcurrent;
				endmacro;
			}
			
			if( column==0 && lineno==1 )
			{
				moveto #org_x, #org_y;
				call selcurrent;
				endmacro;
			}
		}
	}
	
//
// ) → 対応する ( に移動　＆選択
//
	escape;
	
	beginsel;
	movetolineno #startsel_x, #startsel_y;
	endsel;
	
enabledraw;
endmacro;


//
// 開始位置が最外郭の ( であった場合、
// その () 内を選択するサブルーチン。
//
selcurrent:
	if( #startcode == ')' )
	{
		left;
		
		#point = 1;
		while( #point > 0 )
		{
			left;
			
			#ccode = colorcode & 0x0000001f;
			if( (#ccode != 3) && (#ccode != 20) )
			{
				if( code == ')' )
				{
					#point = #point + 1;
				}
				if( code == '(' )
				{
					#point = #point - 1;
				}
				
				if( (code == '}') || (code == '{') || (code == eof) || (code == ';') )
				{
					movetolineno #org_x, #org_y;
					endmacro;
				}
				if( column==0 && lineno==1 )
				{
					moveto #org_x, #org_y;
					call selcurrent;
					endmacro;
				}
			}
		}
		right;
		
		beginsel;
		movetolineno #org_x -1, #org_y;
		endsel;
	}
	
endmacro;

