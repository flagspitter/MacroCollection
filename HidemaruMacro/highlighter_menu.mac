//  多機能蛍光ペン highlighter_menu.mac
//  選択範囲や同じ単語に、一時的に色をつけます
//  メニュー版

//////////////////////////////////////////////////////////////////////////////
// カスタマイズ用
//////////////////////////////////////////////////////////////////////////////
setcompatiblemode 0x2020F; // 部分編集等を維持、検索結果を選択
#type = 0; // 編集時・・・ 0:維持 1:分裂 2:消去

// 同時押し用のキー割当を変えたい場合、ここを変更
##allWord    = iskeydown( 0x12 ); // 全ての同じ単語 (+Alt)
##lastSearch = iskeydown( 91 );   // 最後の検索結果 (+Windows-key)
##esc        = iskeydown( 0x1b );

// 色を変えたい場合、ここを変更
##colorTable[0] = 0;
##colorTable[1] = 0xa00000;
##colorTable[2] = 0x000080;
##colorTable[3] = 0x700050;
##colorTable[4] = 0x008000;
##colorTable[5] = 0x805000;
##colorTable[6] = 0x007090;
##colorTable[7] = 0x666666;

$$iniFile = currentmacrodirectory + "\\highlighter.ini";
#userData = getininum( $$iniFile, "general", "key" );

//////////////////////////////////////////////////////////////////////////////
// モード選択など
//////////////////////////////////////////////////////////////////////////////
##colorNo = getininum( $$iniFile, "general", "backcol" );

if( selecting )
{
	$menuItem[ 0 ] = "&M  選択範囲をマーク";
	$menuItem[ 1 ] = "&W  選択範囲と同じ単語をマーク)";
}
else
{
	$menuItem[ 0 ] = "&M  カーソル位置の単語をマーク";
	$menuItem[ 1 ] = "&W  カーソル位置と同じ単語をマーク";
}
$menuItem[2] = "&S  前回の検索結果をマーク";
$menuItem[3] = "&C  マークの全消去";
$menuItem[4] = "\x01";
$menuItem[5] = "&1  青に変更";
$menuItem[6] = "&2  赤に変更";
$menuItem[7] = "&3  紫に変更";
$menuItem[8] = "&4  緑に変更";
$menuItem[9] = "&5  水色に変更";
$menuItem[10] = "&6  黄色に変更";
$menuItem[11] = "&7  灰色に変更";
$menuItem[12] = "&0  消しゴムに変更";

if( ##colorNo == 0 )
{
	$menuItem[12] = $menuItem[12] + " 【現在の設定】";
}
else
{
	$menuItem[ ##colorNo + 4 ] = $menuItem[ ##colorNo + 4 ] + " 【現在の設定】";
}
menuarray $menuItem, 13;
##pos = result;

if( ##pos >= 6 )
{
	##colorNo = ##pos - 5;
	if( ##colorNo > 7 )
	{
		##colorNo = 0;
	}
	writeininum $$iniFile, "general", "backcol", ##colorNo;
}
else if( ##pos == 1 )
{
	// カーソル位置 or 選択範囲
	##allWord    = 0; // 全ての同じ単語
	##lastSearch = 0; // 最後の検索結果
}
else if( ##pos == 2 )
{
	// 全単語
	##allWord    = 1; // 全ての同じ単語
	##lastSearch = 0; // 最後の検索結果
}
else if( ##pos == 3 )
{
	// 全単語
	##allWord    = 0; // 全ての同じ単語
	##lastSearch = 1; // 最後の検索結果
}
else if( ##pos == 4 )
{
	// 全単語
	##allWord    = 1; // 全ての同じ単語
	##lastSearch = 1; // 最後の検索結果
	##colorNo = 0;
}
else
{
	; // 何も選択されなかった
}

##backCol = ##colorTable[ ##colorNo ];

if( ( ##pos >= 1 ) && ( ##pos <= 4 ) )
{
	//////////////////////////////////////////////////////////////////////////////
	// 下準備
	//////////////////////////////////////////////////////////////////////////////
	disabledraw;
	
	##orgX = x;
	##orgY = y;
	
	if( ##numDot == 0 )
	{
		##chCol = -1;
	}
	else if( ##esc != 0 )
	{
		##chCol = 0;
	}
	else
	{
		##chCol = 0x666666;
	}
	
	if( ##esc != 0 )
	{
		##disable = 0;
	}
	else if( ( ##backCol == 0 ) && ( ##numDot == 0 ) )
	{
		##disable = 1;
	}
	else
	{
		##disable = 0;
	}
	
	// 範囲選択されていない場合は、カーソル位置の単語単体
	if( !selecting )
	{
		selectword;
	}
	
	//////////////////////////////////////////////////////////////////////////////
	// 本処理
	//////////////////////////////////////////////////////////////////////////////
	if( rectselecting )
	{
		// 矩形範囲選択中である場合、その範囲
		call ColorBox ##chCol, ##backCol, ##disable;
	}
	else if( ##allWord && ##lastSearch && ( ##disable != 0 ) )
	{
		// Ctrl + Wnd + Alt + Num0で全消去
		selectall;
		colormarker;
		title 0, 1;
	}
	else if( ##allWord || ##lastSearch )
	{
		// 選択中単語と同じ単語全てを対象
		call ColorAllWord ##chCol, ##backCol, ##disable, ##lastSearch;
	}
	else
	{
		// 選択中範囲のみ
		if( ##disable == 0 )
		{
			colormarker ##chCol, ##backCol, 11, #type, #userData;
		}
		else
		{
			colormarker;
		}
		escape;
		enabledraw;
		redraw;
	}
	
	//////////////////////////////////////////////////////////////////////////////
	// 後始末
	//////////////////////////////////////////////////////////////////////////////
	escape;
	moveto ##orgX, ##orgY;
	enabledraw;
	redraw;
}

endmacro;
// main ここまで
////////////////////////////////////////////////////////////////////////


//////////////////////////////////////////////////////////////////////////////
// 矩形範囲
// void ColorBox( int chCol, int backCol, int flagDisable )
ColorBox:
{
	##chCol   = ##1;
	##backCol = ##2;
	##flagDisable = ##3;
	
	##x0 = seltopx;
	##y0 = seltopy;
	##x1 = selendx;
	##y1 = selendy;
	##i = seltopy;
	
	// １行ずつ処理
	while( ##i <= ##y1 )
	{
		escape;
		moveto ##x0, ##i;
		beginsel;
		moveto ##x1, ##i;
		
		if( ##flagDisable == 0 )
		{
			colormarker ##chCol, ##backCol, 11, #type, #userData;
		}
		else
		{
			colormarker;
		}
		
		##i = ##i + 1;
	}
	return;
}

//////////////////////////////////////////////////////////////////////////////
// 検索して同じ単語を対象
// void ColorBox( int chCol, int backCol, int flagDisable, int lastSearch )
ColorAllWord:
{
	##chCol   = ##1;
	##backCol = ##2;
	##flagDisable = ##3;
	##lastSearch  = ##4;
	
	// 検索文字列等の退避
	$$searchBuf = searchbuffer;
	##searchOpt = searchoption;
	
	escape;
	gofiletop;
	
	if( ##lastSearch )
	{
		// 前回の検索結果を使用
		finddown2;
	}
	else
	{
		// 選択範囲から取得
		$$str = gettext( seltopx, seltopy, selendx, selendy );
		searchdown2 $$str, word;
	}
	
	// 見つかる限り色付けor色消し
	##i = 0;
	##j = 0;
	while( result == true )
	{
		##j = ##j + selendcolumn - seltopcolumn;
		##i = ##i + 1;
		if( ##flagDisable == 0 )
		{
			colormarker ##chCol, ##backCol, 11, #type, #userData;
		}
		else
		{
			colormarker;
		}
		finddown2;
	}
	
	$$tmp = " [ Hilight ... " + str(##i) + " hit (" + str(##j) + " byte)]";
	title $$tmp, 1;
	title -1, 1;
	
	// 検索文字列等の復帰
	setsearch $$searchBuf, ##searchOpt;
	
	return;
}
